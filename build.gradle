buildscript {
	apply from: 'repositories.gradle'
	repositories localRepositories.source as Closure
	dependencies {
		classpath "uk.co.ceilingcat:gradle.plugins.SemanticVersionsPlugin:${versions_semantic_versions}"
	}
}

plugins {
	id 'org.jetbrains.kotlin.jvm' version "${versions_kotlin}"
	id 'org.jlleitschuh.gradle.ktlint' version "${versions_klint}"
}
apply plugin: 'uk.co.ceilingcat.SemanticVersions'

group = 'uk.co.ceilingcat.rayTracingInOneWeekend'
description = 'Ray Tracing in One Weekend in Kotlin'

dependencies {
    implementation "io.arrow-kt:arrow-core:${versions_arrow}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions_kotlin}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions_junit_jupiter}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions_junit_jupiter}"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8 as String
    }
}

repositories localRepositories.source as Closure

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
    useJUnitPlatform()

    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

setProjectVersion.dependsOn patchIncrement
patchIncrement.onlyIf(new Spec<Task>() {
    boolean isSatisfiedBy(Task task) {
        !task.project.hasProperty('isRelease')
    }
})

setProjectVersion.dependsOn patchIncrement
compileKotlin.dependsOn setProjectVersion
[ktlintMainSourceSetCheck, ktlintTestSourceSetCheck].each { it.dependsOn ktlintFormat }
jar.dependsOn check
